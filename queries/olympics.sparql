# Query 1 - List the names of every gold medallist (excluding duplicates).

PREFIX walls: <http://wallscope.co.uk/ontology/olympics/>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?name
WHERE {
 ?instance walls:athlete ?athlete ;
 walls:medal <http://wallscope.co.uk/resource/olympics/medal/Gold> .
 ?athlete rdfs:label ?name .
}

# Query 2 - List the names of every athlete, with at least one medal, alongside their total number of medals (sorted by the number of medals).

PREFIX walls: <http://wallscope.co.uk/ontology/olympics/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?name (COUNT(?name) As ?noOfMedals)
WHERE {
  ?instance walls:athlete ?athlete ;
            walls:medal   ?medal .
  ?athlete  rdfs:label    ?name .
}
GROUP BY ?name
ORDER BY DESC(?noOfMedals)

# Query 3 - List each country alongside the average height and weight of its athletes (sorted by the average height). 

PREFIX walls: <http://wallscope.co.uk/ontology/olympics/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT ?countryCode
       (AVG(?height) As ?avgHeight)
       (AVG(?weight) As ?avgWeight)
WHERE {
 ?noc dbo:ground ?team ;
      rdfs:label ?countryCode .
 
 ?athlete rdf:type   foaf:Person ;
          dbo:team   ?team ;
          dbo:height ?height ;
          dbo:weight ?weight .
}
GROUP BY ?countryCode
ORDER BY DESC(?avgHeight)

# Query 4 - List each year alongside the oldest Judo competitor in that year

PREFIX walls: <http://wallscope.co.uk/ontology/olympics/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbp: <http://dbpedia.org/property/>
SELECT ?year (MAX(?age) As ?maxAge)
WHERE {
 ?instance walls:games   ?games ;
           walls:event   ?event ;
           walls:athlete ?athlete .
 
 ?event rdfs:subClassOf <http://wallscope.co.uk/resource/olympics/sport/Judo> .
 
 ?games dbp:year ?year .
 
 ?athlete foaf:age ?age .
}
GROUP BY ?year

# Query 5 - List every athlete with "louis" in their name alongside the city and season that they competed in. This query uses regex.

PREFIX walls: <http://wallscope.co.uk/ontology/olympics/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbp: <http://dbpedia.org/property/>
SELECT DISTINCT ?name ?cityName ?seasonName
WHERE {
 ?instance walls:games   ?games ;
           walls:athlete ?athlete .
 
 ?games dbp:location ?city ;
        walls:season ?season .
 
 ?city rdfs:label ?cityName .
 
 ?season rdfs:label ?seasonName .
 
 ?athlete rdfs:label ?name .
 
 Filter (REGEX(lcase(?name),"louis.*"))
}

# Query 6 - Get a list of every city that an athlete from Serbia and Montenegro competed in. Count the number of males and females that ever competed in one of those cities and return these counts.

PREFIX walls: <http://wallscope.co.uk/ontology/olympics/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX noc: <http://wallscope.co.uk/resource/olympics/NOC/>
SELECT ?genderName (COUNT(?athlete) AS ?count)
WHERE {
  ?instance walls:games   ?games ;
            walls:athlete ?athlete .
 
  ?games dbp:location ?city .
 
  ?athlete foaf:gender ?gender .
 
  ?gender rdfs:label ?genderName .
 
  {
    SELECT DISTINCT ?city
    WHERE {
    ?instance walls:games   ?games ;
              walls:athlete ?athlete .
 
    ?athlete dbo:team ?team .
 
    noc:SCG dbo:ground ?team .
 
    ?games dbp:location ?city .
    }
  }
}
GROUP BY ?genderName

# Query 7 - List every sport's name that has a stored team size in DBpedia and return these sizes next to the corresponding sport.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
SELECT ?sportName ?teamSize
WHERE {
 ?sport rdf:type   dbo:Sport ;
        rdfs:label ?sportName .
 
 SERVICE <http://dbpedia.org/sparql> {
 ?dbsport rdfs:label   ?sportName ;
          dbo:teamSize ?teamSize .
 }
}

#QueryFTI - List every athlete with "louis" in their name alongside the city and season that they competed in. This query uses each triplestores full text index feature unlike Q5.

PREFIX luc: <http://www.ontotext.com/owlim/lucene#>
PREFIX walls: <http://wallscope.co.uk/ontology/olympics/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbp: <http://dbpedia.org/property/>
SELECT DISTINCT ?name ?cityName ?seasonName
WHERE {
 ?instance walls:games   ?games ;
           walls:athlete ?athlete .
 
 ?games dbp:location ?city ;
        walls:season ?season .
 
 ?city rdfs:label ?cityName .
 
 ?season rdfs:label ?seasonName .
 
 ?athlete rdfs:label ?name .
 
 ?name luc:myIndex "*louis*" .
}
